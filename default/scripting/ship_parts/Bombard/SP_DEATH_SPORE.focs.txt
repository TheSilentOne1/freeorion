Part
    name = "SP_DEATH_SPORE"
    description = "SP_DEATH_SPORE_DESC"
    class = Bombard
    capacity = 1.0
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 3
    tags = [ "CTRL_BOMBARD_ORGANIC" "PEDIA_PC_BOMBARD" ]
    location = All
    effectsgroups = [
        EffectsGroup    // players can order death spores used on enemies
            scope = And [
                [[BOMBARD_SCOPE]]
                HasTag name = "ORGANIC"
            ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 1
                SetTroops value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1.0"
                        tag = "shippart" data = "SP_DEATH_SPORE"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // half damage if different metabolism than organic
            scope = And [
                [[BOMBARD_SCOPE]]
                Not HasTag name = "ORGANIC"
            ]
            effects = [
                SetPopulation value = Value - 0.5
                SetTroops value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DEATH_SPORE"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use death spores on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                Species
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                /// @content_tag{ORGANIC} Each part owned by Unowned in system will reduce population on one visible populated empire Planet with tag
                HasTag name = "ORGANIC"
            ]
            activation = And [
                Unowned
                InSystem
            ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DEATH_SPORE"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/death-spore.png"

#include "/scripting/common/upkeep.macros"
#include "/scripting/common/priorities.macros"
#include "/scripting/ship_parts/bombard/bombard.macros"