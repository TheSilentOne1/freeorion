Part
    name = "SP_DARK_RAY"
    description = "SP_DARK_RAY_DESC"
    class = Bombard
    capacity = 1.0
    mountableSlotTypes = External
    buildcost = 5 * [[FLEET_UPKEEP_MULTIPLICATOR]] * [[SHIP_PART_COST_MULTIPLIER]]
    buildtime = 3
    tags = [ "CTRL_BOMBARD_PHOTOTROPHIC" "PEDIA_PC_BOMBARD" ]
    location = All
    effectsgroups = [
        EffectsGroup    // players can order dark ray used on enemies
            scope = And [
                [[BOMBARD_SCOPE]]
                HasTag name = "PHOTOTROPHIC"
            ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 1
                SetTroops value = Value - 1
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "1"
                        tag = "shippart" data = "SP_DARK_RAY"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // half damage if different metabolism than phototropic
            scope = And [
                [[BOMBARD_SCOPE]]
                Not HasTag name = "PHOTOTROPHIC"
            ]
            effects = [
                SetPopulation value = Value - 0.5
                SetTroops value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DARK_RAY"
                    ]
                    empire = Target.Owner
            ]

        EffectsGroup    // monsters use dark ray on player planets
            scope = NumberOf number = 1 condition = And [
                Planet
                InSystem id = Source.SystemID
                OwnedBy affiliation = AnyEmpire
                Stealth high = Source.Detection
                /// @content_tag{PHOTOTROPHIC} Each part owned by Unowned in system will reduce population on one visible populated empire Planet with tag
                HasTag name = "PHOTOTROPHIC"
            ]
            activation = And [
                            Unowned
                            InSystem
                         ]
            priority = [[EARLY_POPULATION_PRIORITY]]
            effects = [
                SetPopulation value = Value - 0.5
                GenerateSitRepMessage
                    message = "EFFECT_BIOWEAPON"
                    label = "EFFECT_BIOWEAPON_LABEL"
                    icon = "icons/sitrep/bio_weapon_1.png"
                    parameters = [
                        tag = "planet" data = Target.ID
                        tag = "rawtext" data = "0.5"
                        tag = "shippart" data = "SP_DARK_RAY"
                    ]
                    empire = Target.Owner
            ]
    ]
    icon = "icons/ship_parts/dark-ray.png"

#include "/scripting/common/upkeep.macros"
#include "/scripting/common/priorities.macros"
#include "/scripting/ship_parts/bombard/bombard.macros"
